from Bio.SeqIO.FastaIO import SimpleFastaParser
import glob,os
include: "Common.snake"
include: "Binning.snake"

LIST_BINPATHS=[path.replace("/SCG.fna","") for path in  glob.glob("binning/*/Bin_ini/Bin_*/SCG.fna")]

LIST_INI_BINS=[path.split('/')[3] for path in  glob.glob("binning/*/Bin_ini/Bin_*/SCG.fna")]

rule all : 
  input: expand("desman/{group}/Bin_ini/{bins}/desman_analysis_done",bins=LIST_INI_BINS,group=GROUPS)
  # input: expand("{bin_path}/contigs.fa",bin_path=LIST_BINPATHS)
  # input: expand("annotation/{group}/{group}_SCG.bed",group=GROUPS)
  # input: expand("{bin_path}/selected_sites.tsv",bin_path=LIST_BINPATHS)
  # input: expand("{bin_path}/{sample}.bam",bin_path=LIST_BINPATHS,sample=SAMPLES)
  # input: expand("{bin_path}/{sample}.cnt",bin_path=LIST_BINPATHS,sample=SAMPLES)
  # input: expand("{bin_path}/Desman/freqs_{pipeline}.csv",bin_path=LIST_BINPATHS,pipeline=["sel_var","sergey"])
  # input: expand("{bin_path}/Samples_files/SCG_bed_like.tsv",bin_path=LIST_BINPATHS)
  # input: expand("{bin}/contigs.fa",bin=LIST_BINPATHS)
         # expand("{bin_path}/Desman/Deviance.pdf",bin_path=LIST_BINPATHS),
         # expand("{bin_path}/Desman/haplotype_seqs/done",bin_path=LIST_BINPATHS)


LIST_BINPATH_BY_GROUPS={group:[path.replace("/SCG.fna","") for path in  glob.glob("binning/"+group+"/Bin_ini/Bin_*/SCG.fna")] for group in GROUPS}


checkpoint Get_Bins_contigs :
  input :
    bin="binning/{group}/Bin_ini/clustering_gt"+str(MIN_CONTIG_SIZE)+"_merged.csv",
    fasta="profile/scaffolds/{group}.fasta"
  output: temp(touch("binning/{group}/Bin_ini/bins_contigs_done"))
  params:lambda w:" ".join([path.split("Bin_ini/Bin_")[1] for path in LIST_BINPATH_BY_GROUPS[w.group]])
  shell:"""
  {SCRIPTS}/Split_fasta_by_bin.py {input.fasta} {input.bin} binning/{wildcards.group}/Bin_ini -l {params} -d
  """ 


def bin_contigs_are_extracted(wildcards) :
  meh=checkpoints.Get_Bins_contigs.get(group=wildcards.group)
  output="binning/"+wildcards.group+"/Bin_ini/"+wildcards.bin+"/"+wildcards.bin+".fasta"
  return output


rule Get_Bins_contigs_bed :
  input:  fasta=bin_contigs_are_extracted,
          touch="binning/{group}/Bin_ini/bins_contigs_done"
  output: bin_bed="binning/{group}/Bin_ini/{bin}/Samples_files/{bin}.bed"
  shell: "{SCRIPTS}/WriteBed.py {input.fasta} > {output.bin_bed}"

rule bam_by_bin :
    input: 
        global_bam="profile/scaffolds/{group}/{sample}.sorted.bam",
        bin_bed="binning/{group}/Bin_ini/{bin}/Samples_files/{bin}.bed"
    output: 
      bin_bam="binning/{group}/Bin_ini/{bin}/Samples_files/{sample}.bam"
    threads: THREADS
    shell :"""
    samtools view -b -L {input.bin_bed} {input.global_bam} -@{threads} > {output.bin_bam}
    """

rule index_fasta_files:
  input:
    fasta = bin_contigs_are_extracted
  output:
    fai = "binning/{group}/Bin_ini/{bin}/{bin}.fasta.fai"
  shell: "samtools faidx {input.fasta}"

rule bam_readcount:
    input:
        fasta = bin_contigs_are_extracted,
        fai = "binning/{group}/Bin_ini/{bin}/{bin}.fasta.fai",
        bam_file = "binning/{group}/Bin_ini/{bin}/Samples_files/{sample}.bam",
        bed_file = "binning/{group}/Bin_ini/{bin}/Samples_files/{bin}.bed"
    output: "binning/{group}/Bin_ini/{bin}/Samples_files/{sample}.cnt.gz"
    log:
        "binning/{group}/Bin_ini/{bin}/Samples_files/bam_readcount_{sample}.log"
    shell:"""
        samtools index {input.bam_file} {input.bam_file}.bai
        bam-readcount -l {input.bed_file} -f {input.fasta} {input.bam_file} 2> {log} | gzip > {output}
        """

rule get_SCG_bed :
  input:
      gff="annotation/{file}.gff",
      SCG_file="annotation/{file}_SCG.fna"
  output: 
      all_bed="annotation/{file}.bed",
      SCG_bed="annotation/{file}_SCG.bed"
  run:
      os.system(SCRIPTS+"/Gff_to_bed.py "+input.gff)
      Set_seq={name.split()[0] for name,seq in SimpleFastaParser(open(input.SCG_file))}
      Handle_w=open(output.SCG_bed,'w')
      for line in open(output.all_bed) :
          if line.split()[3] in Set_seq :
              Handle_w.write(line)
      Handle_w.close()

rule get_bed_like_file_for_ExtractCountFreqGenes :
  input:
      SCG_bed="annotation/{group}/{group}_SCG.bed",
      Bin_SCG="binning/{group}/Bin_ini/{path}/SCG.fna"
  output: 
      bedlike="binning/{group}/Bin_ini/{path}/Samples_files/SCG_bed_like.tsv"
  run:
    Dico_SCG={header.split()[0]:header.split() for header,seq in SimpleFastaParser(open(input.Bin_SCG))}
  # NODE_11_length_160076_cov_189.092050_50 COG0552 strand=+
    Handle=open(output.bedlike,"w")
    for line in open(input.SCG_bed) :
      contig,start,end,orf=line.rstrip().split("\t")
      if orf in Dico_SCG :
        cog,strand=Dico_SCG[orf][1:]
        cog,strand=Dico_SCG[orf][1:]
        strand=str((strand=="strand=+")*1-(strand=="strand=-")*1)
        contig="_".join(orf.split("_")[:-1])
        Handle.write(",".join([cog,contig,start,end,orf,strand])+"\n")  
    Handle.close()

rule extract_counts:
    input:
        all_counts = lambda wildcards: expand("{path}/{sample}.cnt.gz",sample=SAMPLES,path=wildcards.path),
        SCG_bedlike="{path}/SCG_bed_like.tsv"
    output:
        "{path}/count.csv"
    params:
        input_dir = "{path}"
    log:
        "{path}/extract_counts.log"
    shell:"""
    python {DSCRIPTS}/ExtractCountFreqGenes.py {input.SCG_bedlike} {params.input_dir} --output_file {output} &>> {log}
        """

checkpoint Filter_Variant:
    input:
        "binning/{group}/Bin_ini/{Bin}/Samples_files/count.csv"
    output :
        expand("desman/{{group}}/Bin_ini/{{Bin}}/freqs_{output_file_type}",output_file_type = ["sel_var.csv", "p_df.csv", "q_df.csv", "r_df.csv", "tran_df.csv", "log.txt"])
    params : "desman/{group}/Bin_ini/{Bin}/freqs_"
    log:
        "desman/{group}/Bin_ini/Logs/{Bin}_Filter_variant.log"
    shell:"""
        Variant_Filter.py {input} -o {params} -p -m {MIN_COV_DESMAN} 2>{log}
        """

rule run_desman:
    input:
        sel_var="desman/{group}/Bin_ini/{Bin}/freqs_sel_var.csv",
        err="desman/{group}/Bin_ini/{Bin}/freqs_tran_df.csv"
    output:
        "desman/{group}/Bin_ini/{Bin}/Run_{g}_{r}/fit.txt"
    params:"desman/{group}/Bin_ini/{Bin}/Run_{g}_{r}"
    log:
        "desman/{group}/Bin_ini/Logs/Run_{g}_{r}/{Bin}_{g}_{r}.log"
    shell:"""
      desman {input.sel_var} -e {input.err} -o {params} -m {MIN_COV_DESMAN} -i 100 -g {wildcards.g} -s {wildcards.r} &> {log}
      """


rule Desman_fit:
    input: expand("desman/{{group}}/Bin_ini/{{Bin}}/Run_{g}_{r}/fit.txt",g=range(1,DESMAN_HAPLOTYPE_NB+1),r=range(DESMAN_REPEAT))
    output: "desman/{group}/Bin_ini/{Bin}/Deviance.csv"
    shell : """
        cat $( ls desman/{wildcards.group}/Bin_ini/{wildcards.Bin}/Run_*/fit.txt) | cut -d"," -f2- > {output}
        sed -i '1iH,G,LP,Dev' {output}
    """

rule Desman_plot:
    input: deviance_file="{path}/Deviance.csv",
    output: "{path}/Deviance.pdf"
    shell : """
        {DSCRIPTS}/PlotDev.R -l {wildcards.path}/Deviance.csv -o {output} 2> /dev/null
    """

rule Haplo_seqs:
    input: deviance_file="desman/{group}/Bin_ini/{Bin}/Deviance.pdf",
    output: "desman/{group}/Bin_ini/{Bin}/best_run.txt"
    params: "desman/{group}/Bin_ini/{Bin}"
    shell : """
        python {DSCRIPTS}/resolvenhap.py {params}/Run > {params}/best_run.txt
        
        cut -d"," -f 1 < {params}/freqs_sel_var.csv | sort | uniq | sed '1d' > {params}/coregenes.txt

        if [ ! -d {params}/haplotype_seqs ]; then 
            mkdir {params}/haplotype_seqs
        fi
        
        python {DSCRIPTS}/GetVariantsCore.py profile/scaffolds/{wildcards.group}.fasta binning/{wildcards.group}/Bin_ini/{wildcards.Bin}/Samples_files/SCG_bed_like.tsv $(cut -d"," -f5 {params}/best_run.txt) {params}/coregenes.txt -o {params}/haplotype_seqs/
    """
    

def do_we_need_to_run_desman(wildcards) :
  freq_sel_var_file=checkpoints.Filter_Variant.get(group=wildcards.group,Bin=wildcards.Bin).output[0]
  nb_line=len([line for line in open(freq_sel_var_file)])
  if nb_line>1 :
    return "desman/" + wildcards.group +"/Bin_ini/" + wildcards.Bin + "/best_run.txt"
  else :
    return freq_sel_var_file

rule Is_Desman_done :
  input : do_we_need_to_run_desman
  output: touch("desman/{group}/Bin_ini/{Bin}/desman_analysis_done")
  shell :"echo Analysis done > /dev/null"

